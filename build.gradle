plugins {
    id "java-library"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = rootProject.maven_group
version = rootProject.release_version
base.archivesName = project.archives_base_name

repositories {
    maven {
        url = "https://jitpack.io/"
    }
    mavenCentral()
}

dependencies {
    compileOnly "org.jetbrains:annotations:26.0.1"
    annotationProcessor(compileOnly("org.projectlombok:lombok:1.18.34"))
    implementation "commons-io:commons-io:2.16.1"
    implementation "me.lucko:jar-relocator:1.7"

    testImplementation platform('org.junit:junit-bom:5.10.3')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testAnnotationProcessor(testCompileOnly("org.projectlombok:lombok:1.18.34"))
}

test {
    useJUnitPlatform()

    ignoreFailures true

    testLogging {
        exceptionFormat = "full"
        events "passed", "skipped", "failed"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach { tsk ->
    tsk.options.encoding = "UTF-8"
}

tasks.withType(Test).configureEach { tsk ->
    tsk.systemProperty "file.encoding", "UTF-8"
}

jar {
    manifest {
        attributes "Implementation-Version": project.version
    }
}

compileJava.dependsOn clean

shadowJar {
    archiveClassifier.set "shaded"

    def reloc = { String from, String to ->
        relocate from, to
    }

    reloc "org.apache.commons.io", "xyz.tcbuildmc.common.dependency.runtime.lib.commons_io"
    reloc "me.lucko.jarrelocator", "xyz.tcbuildmc.common.dependency.runtime.lib.jarrelocator"
    reloc "org.objectweb.asm",     "xyz.tcbuildmc.common.dependency.runtime.lib.asm"
}

assemble.dependsOn shadowJar

publishing {
    publications {
        create("MavenJava", MavenPublication) {
            from components.java
            artifactId = project.archives_base_name
        }
    }

    repositories {
        mavenLocal()
    }
}

